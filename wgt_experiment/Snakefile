import os
import pandas as pd

# add scripts dir to path
code_dir = "/home/jsadler/drb-dl-model/river_dl"
shell.prefix("module load analytics cuda100/toolkit/10.0.130 \n \
              run_training -e /home/jsadler/.conda/envs/rgcn --no-node-list ")

from river_dl.preproc_utils import prep_data
from river_dl.postproc_utils import predict_from_file, overall_metrics, combined_reach_specific, combine_csvs
data_dir = "/home/jsadler/drb-dl-model/data/in"
out_dir = "/caldera/projects/usgs/water/iidd/datasci/drb_ml_model/experiments/subset/wgt_experiment"
exper = config['experiment_name']
x_vars = ['seg_rain', 'seg_tave_air', 'seginc_swrad', 'seg_length', 'seginc_potet', 'seg_slope', 'seg_humid', 'seg_elev']
num_replicates = 5


rule all:
    input:
        f"{out_dir}/ex_{exper}/overall_metrics.nc",
        f"{out_dir}/ex_{exper}/overall_reach_metrics.nc"


rule prep_io_data:
    input:
         f"{data_dir}/obs_temp_full",
         f"{data_dir}/obs_flow_full",
         f"{data_dir}/uncal_sntemp_input_output_subset",
         f"{data_dir}/distance_matrix_subset.npz",
         catch_attr = f"{data_dir}/seg_attr_drb.feather"
    output:
        "{outdir}/prepped.npz"
    params:
        test_start_date='2004-09-30',
        n_test_yr=12
    run:
        prep_data(input[0], input[1], input[2], input[3], x_vars,
                  catch_prop_file=input['catch_attr'],
                  pretrain_vars=config['pt_vars'],
                  finetune_vars=config['ft_vars'],
                  reduce_temp_trn=config.get('reduce_temp_trn', 0),
                  reduce_flow_trn=config.get('reduce_flow_trn', 0),
                  exclude_file=config.get('exclude_file'),
                  test_start_date=params.test_start_date,
                  log_q=True,
                  n_test_yr=params.n_test_yr, out_file=output[0])


# this must be a shell command so that the correct GPU libraries are loaded and used
rule train:
    input:
         "{outdir}/ex_{experiment}/prepped.npz"
    output:
        directory("{outdir}/ex_{experiment}/{run_id}/trained_weights/"),
        directory("{outdir}/ex_{experiment}/{run_id}/pretrained_weights/")
    params:
        # getting the base path to put the training outputs in
        # I omit the last slash (hence '[:-1]' so the split works properly
        run_dir=lambda wildcards, output: os.path.split(output[0][:-1])[0],
        flow_in_temp="-q", 
        pt_epochs=200,
        ft_epochs=100
    shell:
         """
         "python {code_dir}/train_model.py -o {params.run_dir} -i {input[0]} -m {output[0]} -p {params.pt_epochs} -f {params.ft_epochs} {params.flow_in_temp}" 
         """

rule make_predictions:
    input: 
        "{outdir}/ex_{experiment}/{run_id}/trained_weights/",
        "{outdir}/ex_{experiment}/prepped.npz",
    params:
        hidden_size=20,
        half_tst=True,
    output:
        "{outdir}/ex_{experiment}/{run_id}/{partition}_preds.feather",
    run:
        predict_from_file(input[0], input[1], params.hidden_size,
                          wildcards.partition, output[0], flow_in_temp=True,
                          half_tst=params.half_tst)


rule calc_overall_metrics:
    input:
         "{outdir}/ex_{experiment}/{run_id}/{partition}_preds.feather",
         expand("{data_dir}/obs_{variable}_full", data_dir=data_dir, allow_missing=True)
    output:
         "{outdir}/ex_{experiment}/{run_id}/{partition}_{variable}_metrics.csv"
    run:
         overall_metrics(input[0], input[1], wildcards.variable, wildcards.partition, output[0])


rule combined_metrics:
    input:
        expand("{outdir}/ex_{experiment}/{run_id}/{partition}_{variable}_metrics.csv", partition=['trn', 'tst'],
               variable=['temp', 'flow'], allow_missing=True)
    output:
         "{outdir}/ex_{experiment}/{run_id}/combined_metrics.csv"
    run:
        combine_csvs(input, output[0])


def combine_replicate_csvs(csvs, seg_id_idx=False):
    df_list = []
    for i, metric_file in enumerate(csvs):
        df = pd.read_csv(metric_file)
        df['run_id'] = i
        df_list.append(df)
    df = pd.concat(df_list, axis=0)
    df['exp_id'] = exper
    idx_cols = ['run_id', 'variable', 'partition', 'exp_id']
    if seg_id_idx:
        idx_cols.append('seg_id_nat')
    ds = df.set_index(idx_cols).to_xarray()
    return ds

rule combined_run_metrics:
    input:
         expand("{outdir}/ex_{experiment}/{run_id}/combined_metrics.csv",
                run_id=list(range(num_replicates)), allow_missing=True)
    output:
         "{outdir}/ex_{experiment}/overall_metrics.nc"
    run:
        ds = combine_replicate_csvs(input)
        ds.to_netcdf(output[0])


rule calc_reach_specific_metrics:
    input: 
         "{outdir}/ex_{experiment}/{run_id}/trn_preds.feather",
         "{outdir}/ex_{experiment}/{run_id}/tst_preds.feather",
         f"{data_dir}/obs_temp_full",
         f"{data_dir}/obs_flow_full",
    output:
        "{outdir}/ex_{experiment}/{run_id}/reach_metrics.csv",
    run:
        combined_reach_specific(input[0], input[1], input[2], input[3], output[0])


rule combined_run_reach_metrics:
    input:
         expand("{outdir}/ex_{experiment}/{run_id}/reach_metrics.csv",
                run_id=list(range(num_replicates)), allow_missing=True)
    output:
         "{outdir}/ex_{experiment}/overall_reach_metrics.nc"
    run:
        ds = combine_replicate_csvs(input, seg_id_idx=True)
        ds.to_netcdf(output[0])
